buildscript {
    repositories {
        jcenter()
        google()
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidToolsBuildGradle"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.3'
    }
}

plugins {
    id 'com.android.library'
    id 'org.smartregister.gradle.jarjar'
}

description = 'OpenSRP stock client library'

jacoco {
    toolVersion = '0.8.0'
}

android {
    useLibrary 'org.apache.http.legacy'
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultPublishConfig !isReleaseBuild() ? "debug" : "release"

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        multiDexEnabled true
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    // avoid errors with message 'Duplicate files copied in APK ...'
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE.md'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/ASL2.0'
        exclude 'androidsupportmultidexversion.txt'
        exclude("META-INF/*.kotlin_module")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

allprojects {
    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
        mavenLocal()
    }
}

dependencies {
    implementation ('org.smartregister:opensrp-client-native-form:1.14.4.2-SNAPSHOT@aar') {
        transitive = true
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'org.jacoco', module: 'org.jacoco.agent'
        exclude group: 'org.android.support', module: 'cardview-v7'
    }

    api ('org.smartregister:opensrp-client-core:2.0.8-SNAPSHOT@aar') {
        transitive true
        exclude group: 'com.github.bmelnychuk', module: 'atv'
        exclude group: 'com.github.johnkil.print', module: 'print'
        exclude group: 'com.ibm.fhir', module: 'fhir-model'
    }

    jarJar 'com.ibm.fhir:fhir-model:4.2.3'

    implementation fileTree(dir: "./build/libs", include: ['*.jar'])

    api 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.jjoe64:graphview:4.2.1'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation 'org.apache.maven:maven-ant-tasks:2.1.3'

    // PowerMock
    def powerMockVersion = '1.7.4'
    testImplementation "org.powermock:powermock-module-junit4:$powerMockVersion"

    androidTestImplementation ('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}

apply from: '../maven.gradle'
